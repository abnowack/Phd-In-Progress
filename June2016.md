# Cython Notes

Based on [Cython: A Guide by Programmers by Kurt Smith](http://shop.oreilly.com/product/0636920033431.do)

### Compiler Directives
List is [available here](http://docs.cython.org/src/reference/compilation.html#compiler-directives)

Can be implemented in a specific section of code using a context manager
```
from cython cimport boundscheck, wraparound

def foo(...):
  ...
  
  with boundscheck(False), wraparound(False):
    for i in range(N):
      ...
```
Or used as a decorator around an entire function or method
```
from cython cimport boundscheck, wraparound

@boundscheck(False)
@wraparound(False)
def foo(...)
  ...
```
Throughout an extension module file by placing it at the very top
```
# cython: boundscheck=False
# cython: wraparound=False

def foo(...)
  ...
```
And lastly globally when compiling Cython code

`cython --boundscheck=False --wraparound=False foo.pyx`

### Compiling a Cython Project

### Statically Declared C Types

Speed up is mainly obtained by converting dynamic Python objects into statically defined C types. Done by using the `cdef` keyword.
```
cdef int a, b = 2
cdef float c = 3.14
cdef int *int_pointer
```
Pointers are supported, however * can't be used for dereferencing. The work-around is to use the zero-index array format
```
cdef double pi, *p_pi
p_pi = &pi
p_pi[0] = 3.14
```

Also struct members when using struct pointers are accessed using the dot operator (.) instead of the arrow operator (->).
```
cdef foo *p_foo = make_foo()
cdef int bar = p_foo.a + p_foo.b
```

Can statically declare built-in Python types as well
```
cdef list foo_list, bar_list
```
As well as mixing static and dynamic variables such as creating a dynamic tuple
```
cdef int a, b, c
int_tuple = (a, b, c)
```

However statically declaring Python types will avoid the expensive type and method lookups which is where most speed improvements are made.

One last note: Don't reference temporary Python objects, as they will be deleted through the garbage collector
```
b1 = b"A btye string"
b2 = b"and a second one"
cdef char *buf = b1 + b2
```
Instead create a temporary object, which may be dynamically or statically typed
```
# dynamic
tmp = s1 + s2
# static
cdef bytes tmp = s1 + s2

cdef char *buf = tmp
```

### Three Types of Functions
- def function_name(args)

  Python function compiled as type builtin_function_or_method which is not a C-function. It is a python extensible object with exceptions that attributes like __name__ are not modifiable in Cython. Return values are python objects. Usually 2-3 times performance improvements. Handles Exceptions automatically.

- cdef function_name(cdef args)

  Compiled as a C-function with C arguments and C return values. Can be called by other Cython code, but not by Python directly. Must wrap it in a def wrapper_function in the Cython code for external Python code to use. Must be aware of Python -> C type conversion and overflow errors.

- cpdef function_name(cdef args)

  Combines the functionality of two previous methods by compiling the function as a C function but provides a Python funciton wrapper of the same name. No performance difference compared to cdef function_name. However the return type is limited to those compatible with both C and Python. Return types of void, C pointers and C arrays are excluded.

### Passing Exceptions in Cython Functions

For cdef and cpdef, exceptions can be handled by having Cython either always check if an exception was raised, check only if it returns a specific value, or if the return value does not occur normally raise the exception everytime if that value is returned.

cpdef function(int i) except *:   # Always check global exception state if exception raised

cpdef function(int i) except? -1: # While -1 may be a valid return value, check if exception was raised if -1 is returned

cpdef function(int i) except -1:  # -1 is not possible as a return value normally, no exception check needed

### Type Casting

In Cython C-style type casts are done by using angle brackets <int> instead of paranthesis (int)

cdef int *ptr_i = <int*>var

Can even convert types to builtin Python types such as list

cdef list cast_list = <list>some_object

However if some_object is not compatible will get a generic SystemError message when trying to use list methods on cast_list. A solution is to ask Cython to check the type by using <list?> where ? indicates to check the type. A TypeError exception will be raised instead of the types are not compatible.

### Structs, Unions, Enums

In C:
```C
struct foo {
  int a;
  double b;
};

typedef struct typed_foo {
  int c;
  double d;
} typed_foo;

typed_foo bar;
```
In Cython:
```
cdef struct foo:
  int a
  double b

ctypedef struct typed_foo:
  int c
  double d

cdef typed_foo bar
```
Can use different initializations
```
cdef typed_foo bar = typed_foo(4, 3.14)
cdef typed_foo bar = typed_foo(c=4, d=3.14)

cdef typed_foo bar
bar.c = 4
bar.d = 3.14

cdef typed_foo bar = {'c': 4, 'd': 3.14}
```

While anonymous nested structs aren't supported, can use nested structs by assigning dummy names

In C:
```
struct nested {
  int outer_a;
  struct inner_ {
    int inner_a;
  } inner;
};
```

In Cython:
```
cdef struct _inner:
  int inner_a

cdef struct nested:
  int outer_a
  _inner inner
```

Enums can be declared in several ways, and may be anonymous (which are basically compile time integer constants).

```
cdef enum PRIMARY_COLOR:
  RED = 1
  YELLOW = 3
  BLUE = 5

cdef enum SECONDARY_COLOR:
  ORANGE, GREEN, PURPLE

cdef enum:
  SEED = 37
```

### Typedefs

Must be declared at the file scope, not inside functions.

```
ctypedef double real_type
ctypedef long integral_type
```

### Using For/While Loops Efficiently

By static typing the range argument and index variable, Cython will generate a direct C for/while loop everytime.
```
cdef unsigned int i, n = 100
for i in range(n):
  ...
```

For looping over builtin types and NumPy objects, statically typing the loop variable may introduce more overhead and slower code.

### Preprocessor DEFinitions

```
DEF PI = 3.1415
```

Supports IF/ELIF/ELSE statements. UNAME_[SYSNAME, RELEASE, VERSION, MACHINE, NODENAME] typedefs aredefined by Cython.

## Classes and Extension Types

Cython is able to compile Python classes without modification. They behave the same and have a small speed improvement. These compiled classes are referred to as extension types.

To get a speed increase, we must declare a class as follows

```
cdef class Particle
  """Docstring for extension type."""
  cdef double mass, position, velocity
  def __init__(self, m, p, v):
    self.mass = m
    self.position = p
    self.velocity = v
```

Where cdef is added before class, and all attributes assigned in `__init__` are declared in the class body.

Unlike Python classes, by default extension types do not allow modification or reading of class attributes directly. Additionally new attributes cannot be added at runtime.

However attribute access can be controlled through two keywords

cdef readonly double mass - Allows attribute read access at the Python level
cdef public double mass - Allows attribute read and write access at the Python level

There are also cases where a Python class may be constructed which does not call `__init__` or calls it multiple times. Cython provides a `__cinit__` method which will only be called once and is useful for C-style allocations of memory such as 2D arrays. Deallocation can then be performed using `__dealloc__(self)`.

Extension Type methods can also be converted into static C-style functions through cdef and cpdef with the same improvements and differences as for functions.

It's also possible to subclass Extension types either in Cython, or in Python through the usual inheritance syntax.

While Cython can't tell what a dynamically typed object such as a Python-level class is, we can call methods on it by doing an explicit cast on it first
```
# convert using temporary variable
cdef Particle static_p = p
print static_p.get_momentum()
# direct inline cast, no type checking
print (<Particle>p).get_momentum()
# add type checking
print (<Particle?>p).get_momentum()
```

:warning: Cython allows functions which accept extension types to have the `None` type passed in creating a segmentation error. The solution is to use the syntax `def foo(Bar b not None)` if this can occur to do a type check for none. There is also a noncheck compiler directive available to use.

Property methods can by defined in Cython, by calling the property and it's `__get__` and `__set__` and also `__del__` attributes.

```
cdef class Foo:
  property bar:
    def __get__(self):
      ...
    def __set__(self):
      ...
```

Similarly many special methods can be done in Cython. The arithmetic operators are a bit weird, instead of `__add__` and `__radd__` methods, what we do is implement only `__add__` but the arguments can have the current type be the first or second argument depending on the addition order and which one has implemented `__add__`.

Cython Add Example:
```
cdef class Foo:
  cdef int data
  def __init__(self, d):
    self.data = d
  def __add__(x, y):
    if isinstance(x, Foo):
      if isinstance(y, int):
        return (<E>x).data + y
    elif isinstance(y, E):
      if isinstance(x, int):
        return (<E>y).data + x
    else:
      return NotImplemented
```

Most of the other arithmetic special functions are defined similarly.

Arithmetic comparisons such as <, >=, ==, etc. are not done with `__eq__`, `__lt__` etc. special methods but instead with a single comparison function `__richcmp__(x, y, op)` where `op` defines the comparison. So a test must be done on `op` in the definition. 

For iterables, define `__iter__` to make it iterable. However to make it an iterator, define the `__next__` special method.

### NumPy

Cython supports NumPy through the [Buffer Protocol](https://docs.python.org/2/c-api/buffer.html). This is a C-level protocol which is not NumPy specific and with a defined API. Builtin str, bytes, the array.array and ctypes.array types all support the buffer protocol.

Going the other way, from the buffer protocol to python, we can use the memoryview() built-in type which allows iteration and slicing on any object that supports the buffer protocol.

In between, Cython supports the typed memoryview which overlaps with the Python level memoryview type.

In Cython, typed memory views are passed as args and declared based on specifying: type, number of dimensions, contiguous or strided, C or Fortran format, and direct or indirect access. By default arrays are in C format and strided. The advantage of contiguous arrays are performance improvements at the expense of restricting slicing.

Examples

`cdef double[:] foo` - 1-Dimensional array of doubles

`cdef int[:, :] foo` - 2-Dimensional array of ints

`cdef int[:, ::1] foo` - 2-Dimensional array of contiguous ints in C format

`cdef int[::1, :] foo` - 2-Dimensional array of contiguous ints in Fortran format

The indication of contiguous or not is done by specifying the last dimension as ::1 for C-format or the first dimension for Fortran format.

These can be passed as args, or assigned to memoryview types such as NumPy arrays
```
# assign a strided array
cdef float[:, :, :] foo
arr = np.ones((10, 10, 10), dtype=np.float32)
foo = arr[:8:2, ::2, 2::-2]

# contiguous only
cdef float[:, :, ::1] bar
bar = np.ones((10, 10, 10), dtype=np.float32)
```

Manipulation, assignment, modification, etc. of typed memoryviews are identical to NumPy with the except that you can't use NumPy ufuncs on them which means that only simple assignment is available for broadcasting. However np.asarray supports memoryview without copying data for easy manipulation.
